_realname=suscan
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r1243.b396020
pkgrel=1
pkgdesc="Channel scanner based on sigutils library (mingw-w64)"
arch=('any')
provides=("${MINGW_PACKAGE_PREFIX}-suscan")
conflicts=("${MINGW_PACKAGE_PREFIX}-suscan")
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/BatchDrake/suscan"
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-sigutils-git"
         "${MINGW_PACKAGE_PREFIX}-volk"
         "${MINGW_PACKAGE_PREFIX}-soapysdr"
         "${MINGW_PACKAGE_PREFIX}-fftw"
         "${MINGW_PACKAGE_PREFIX}-libsndfile"
         "${MINGW_PACKAGE_PREFIX}-libxml2")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-make"
             "git")
source=("${pkgname}::git+https://github.com/BatchDrake/suscan.git"
        "001-soapy-include.patch")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd "${pkgname}"

  git submodule init
  git submodule update

  patch -Np1 -i "${srcdir}"/001-soapy-include.patch
}

pkgver() {
    cd "${pkgname}"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"

  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -G"MinGW Makefiles" \
      ../${pkgname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build . -v -j1
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .

  # Fix dll location
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin/"
  mv "${pkgdir}${MINGW_PREFIX}/lib/"*.dll "${pkgdir}${MINGW_PREFIX}/bin/"
}
