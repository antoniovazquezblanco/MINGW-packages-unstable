_realname=hackrf
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=2024.02.1.r49.g71bc3940
pkgrel=1
pkgdesc="Low cost software radio platform (mingw-w64)"
arch=('any')
conflicts=("${MINGW_PACKAGE_PREFIX}-hackrf")
provides=("${MINGW_PACKAGE_PREFIX}-hackrf")
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/greatscottgadgets/hackrf"
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-libusb"
         "${MINGW_PACKAGE_PREFIX}-fftw")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-make"
             "${MINGW_PACKAGE_PREFIX}-dfu-util"
             "git")
source=("${pkgname}::git+https://github.com/greatscottgadgets/hackrf.git"
        "001-libusb.patch"
        "002-fftw.patch")
sha256sums=('SKIP' 'SKIP' 'SKIP')

prepare() {
  cd "${pkgname}"

  git submodule update --init --recursive
  
  patch -Np1 -i "${srcdir}"/001-libusb.patch
  patch -Np1 -i "${srcdir}"/002-fftw.patch
}

pkgver() {
    cd "${pkgname}"

    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

build() {
  cd "${pkgname}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  mkdir -p "${srcdir}/build-${MSYSTEM}-host" && cd "${srcdir}/build-${MSYSTEM}-host"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -G"MinGW Makefiles" \
      ../${pkgname}/host

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}-host"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}
