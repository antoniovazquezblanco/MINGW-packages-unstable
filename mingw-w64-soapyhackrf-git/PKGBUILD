_realname=soapyrhackrf
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r117.6c0c33f
pkgrel=1
pkgdesc=" SoapySDR HackRF module (mingw-w64)"
arch=('any')
provides=("${MINGW_PACKAGE_PREFIX}-soapyhackrf")
conflicts=("${MINGW_PACKAGE_PREFIX}-soapyhackrf")
mingw_arch=('mingw64' 'ucrt64' 'clang64')
url="https://github.com/pothosware/SoapyHackRF"
license=('spdx:GPL-3.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-soapysdr"
         "${MINGW_PACKAGE_PREFIX}-hackrf")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-make"
             "git")
source=("${pkgname}::git+https://github.com/pothosware/SoapyHackRF.git")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"
}

pkgver() {
    cd "${pkgname}"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"

  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    "${MINGW_PREFIX}"/bin/cmake.exe \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      "${extra_config[@]}" \
      -G"MinGW Makefiles" \
      ../${pkgname}

  "${MINGW_PREFIX}"/bin/cmake.exe --build .
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}"/bin/cmake.exe --install .
}
